import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their MD5 checksums.
        def filesToDownload = [
            ["url": "https://github.com/FrozenRamune/libmpv_android/releases/download/vnext/default-arm64-v8a.jar", "md5": "08ab0396fc0a10df2c1ebd79e89a09fb", "destination": file("$buildDir/libmpv/default-arm64-v8a.jar")],
            ["url": "https://github.com/FrozenRamune/libmpv_android/releases/download/vnext/default-armeabi-v7a.jar", "md5": "509790bf6dd09ba7f423585de4a5b80e", "destination": file("$buildDir/libmpv/default-armeabi-v7a.jar")],
            ["url": "https://github.com/FrozenRamune/libmpv_android/releases/download/vnext/default-x86_64.jar", "md5": "8a9e78fafade3264fb1370af7c233302", "destination": file("$buildDir/libmpv/default-x86_64.jar")],
            ["url": "https://github.com/FrozenRamune/libmpv_android/releases/download/vnext/default-x86.jar", "md5": "ef3d310d9746995415a09df53fe2c859", "destination": file("$buildDir/libmpv/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedMD5 != fileInfo.md5) {
                    destFile.delete()
                    println "MD5 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedMD5 != fileInfo.md5) {
                    throw new GradleException("MD5 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)